---
layout: post
title: "Simplificando la Programaci√≥n en Business Central: Un Viaje a Trav√©s de Funciones Personalizadas para la Gesti√≥n de URLs"
summary: "Ad√©ntrate en el universo de funciones personalizadas, desde la creaci√≥n de la base de la URL hasta la extracci√≥n de consultas din√°micas. Este art√≠culo te guiar√° a trav√©s de un viaje emocionante, liber√°ndote de la monoton√≠a y brind√°ndote el control total sobre tus peticiones API."
author: "Esteve Sanpons"
date: "2024-01-14 04:00:00 +0200"
#cSpell:disable
category: ["Business_Central", "API"]
thumbnail: /assets/img/posts/codeunit-url-api/imagen01.png
permalink: /blog/codeunit-url-api/
custom_type: Blog
#cSpell:enable
---

Hola a todos, en esta nueva entrega de la serie de objetos API de Business Central vamos a hablar sobre c√≥mo crear funciones para facilitar la gesti√≥n de URLs desde el c√≥digo.

Hoy veremos como crear en Business Central unas funciones para facilitar el manejo de la URL y sus infinitas opciones des del c√≥digo.

Con estas funciones podremos evitar tener que escribir a mano las infinitas opciones que ofrecen las URLs, lo que agilizar√° nuestro trabajo y nos permitir√° centrarnos en lo que realmente importa.

<br>

Por fin hoy nos pondremos a desarrollar en Business Central. As√≠ que, ¬°Vamos manos a la obra! üòç

<br>

## Base URL

Primero, creamos una nueva Codeunit con una funci√≥n para inicializar la base de la URL. Esta funci√≥n guarda en una variable de tipo texto el resultado y la variable que hemos creado se llama "BaseURL". Le pedimos a la funci√≥n el nombre del servicio del objeto API, que se corresponde con el EntitySetName.

Esto guardara en una variable de tipo texto el resultado. Esta variable en el ejemplo que estamos creado es la variable ‚ÄúBaseURL‚Äù

Como vemos le pedimos a la funci√≥n el EntitySetName que seria el nombre del servicio del objeto tipo API.

```javascript
codeunit 51102 "MyCreateUrl"
{
    procedure InitBaseURLPageAPI(EntitySetName: Text)
    var
        URLBaseLbl: Label 'http://bclast:7048/BC/api/', Locked = true;
        APIPublisherLbl: Label 'mycompany/', Locked = true;
        APIGroupLbl: Label 'sales/', Locked = true;
        VersionLbl: Label 'v2.0/', Locked = true;
        URLCompanieLbl: Label 'companies(33e38fba-8f98-ec11-bb87-000d3a2690e9)/', Locked = true;
    begin
        BaseURL := URLBaseLbl + APIPublisherLbl + APIGroupLbl + VersionLbl + URLCompanieLbl + EntitySetName;
    end;

}
```

<br>

## Llamar a funciones en objetos API

Tambi√©n hemos creado una funci√≥n para llamar a las funciones externas de los objetos API. Como ya hemos visto, se pueden llamar a las funciones de los objetos API a√±adi√©ndolos en la URL. Con esta funci√≥n solo tendremos que indicarle el nombre de la funci√≥n y lo a√±adir√° autom√°ticamente a la URL.

```javascript
    procedure SetUrlFuntionPageAPI(NameFunction: Text)
    var
        FunctionLbl: Label '/Microsoft.NAV.', Locked = true;
    begin
        NameFunction := FunctionLbl + NameFunction;
        UrlFuntionPageAPI := NameFunction;
    end;
```

<br>

Tal como vimos anteriormente se puede llamar a las funciones de los objetos de tipo API a√±adi√©ndolos en la URL.

Con esta funci√≥n solo tendremos que notificarle el nombre de la funci√≥n y lo a√±adir√° a la URL.

<br>

## Filtro de la Key del objeto tipo API

Otra funci√≥n que hemos creado es para a√±adir el valor de la clave en la URL de petici√≥n. Esto es √∫til para a√±adir el dato del "ODataKeyFields" que hemos visto en las URLs.

```javascript
    procedure SetUrlFilterKeyPageAPI(ValueEquals: Variant)
    var
        char39: Char;
        TxtValueEquals: Text;
    begin
        char39 := 39;
        TxtValueEquals := Format(ValueEquals);

        case true of
            ValueEquals.IsText:
                TxtValueEquals := char39 + TxtValueEquals + char39;
            ValueEquals.IsDate:
                TxtValueEquals := Format(ValueEquals, 0, '<Year4>-<Month,2>-<Day,2>');
        end;

        TxtValueEquals := StrSubstNo('(%1)', TxtValueEquals);
        UrlFilterKeyPageAPI := TxtValueEquals;
    end;
```

<br>

Pues en esta funci√≥n lo que estamos haciendo es, precisamente eso, a√±adir el valor de la clave en la URL de petici√≥n.

<br>

## Mostrar un m√°ximo de registros

Para mostrar un n√∫mero m√°ximo de registros por cada llamada, hemos creado una funci√≥n que a√±ade el m√°ximo que le digamos a una variable que despu√©s a√±adiremos a la URL.

```javascript
    procedure SetUrlTop(Top: Integer)
    var
        FilterLbl: Label '$top=', Locked = true;
    begin
        InitConsultationOptionsURL(UrlTop, FilterLbl, MyLogicalOperators::" ");
        UrlTop += Format(Top);
    end;
```

<br>

Para esto hemos creado esta funci√≥n que a√±adir√° el m√°ximo que le digamos a una variable que despu√©s a√±adiremos a la URL.

<br>

## Inicio Operadores de la URL

A partir de este momento necesitamos una funci√≥n para determinar si es el primer par√°metro de o se han a√±adido otros. Para ello hemos creado una funci√≥n que a√±adiremos antes de cualquier llamada de las siguientes funciones. Esta funci√≥n a√±ade el operador l√≥gico correspondiente en la URL seg√∫n corresponda.

```javascript
    local procedure InitConsultationOptionsURL(var TextFilterUrl: Text; FilterTxt: Text; MyEnumUrlFilter: Enum MyLogicalOperators)
    var
        Pos: Integer;
        AndLbl: Label ' and ', Locked = true;
        OrLbl: Label ' or ', Locked = true;
        "?Lbl": Label '?', Locked = true;
        "&Lbl": Label '&', Locked = true;
        PosicionInicialLbl: Label '?$', Locked = true;
    begin
        RepeatControl(TextFilterUrl, FilterTxt, MyEnumUrlFilter);

        if IsTheQuestionMarkThere then
            TextFilterUrl += "&Lbl"
        else begin
            TextFilterUrl += "?Lbl";
            IsTheQuestionMarkThere := true;
        end;

        Pos := StrPos(TextFilterUrl, FilterTxt);
        if Pos = 0 then
            TextFilterUrl += FilterTxt
        else begin
            TextFilterUrl := PadStr(TextFilterUrl, StrLen(TextFilterUrl) - 1);
            TextFilterUrl += format(MyEnumUrlFilter);
        end;
    end;
```

<br>

Como vemos lo primero que pondr√° ser√° o el ‚Äú?‚Äù o el ‚Äú&‚Äù si es el primero operador pondr√° el ‚Äú?‚Äù y si no pondr√° el ‚Äú&‚Äù.

Despu√©s verificara si ese operador ya esta en la URL si estuviera se puede configurar para que a√±ada un ‚Äúand‚Äù o un ‚Äúor‚Äù para ese operador.

<br>

## Operador L√≥gico

Adem√°s, hemos creado un enum para determinar si los operadores dentro del mismo tipo ser√°n con "And" o con "or".

```javascript
enum 51100 "MyLogicalOperators"
{
    Extensible = true;

    value(1; " ") { }
    value(2; " and ") { }
    value(3; " or ") { }
}
```

<br>

## Seleccionar campos en concreto

Otra funci√≥n que hemos creado es para seleccionar campos espec√≠ficos dentro de una petici√≥n de WebService. Esto nos permite ver solo ciertos campos de los que nos ofrecen

```javascript
    procedure SetUrlSelectFields(NameFields: Text)
    var
        FilterLbl: Label '$select=', Locked = true;
    begin
        RepeatControl(UrlSelectFields, FilterLbl, MyLogicalOperators::" ");

        InitConsultationOptionsURL(UrlSelectFields, FilterLbl, MyLogicalOperators::" ");
        UrlSelectFields += NameFields;
    end;
```

<br>

Esta funci√≥n a√±ade a la URL los campos que se quieran mostrar.

<br>

## Mostrar subp√°gina

Tambi√©n hemos creado una funci√≥n para mostrar subp√°ginas. En algunos casos, como en los pedidos, los registros tienen una cabecera y una lista de l√≠neas, por lo que las l√≠neas est√°n dentro de la p√°gina de cabecera. El resultado lo a√±adimos a una variable global para ser insertada en la URL al final.

```javascript
    procedure SetUrlSubPage(NameSubPage: Text)
    var
        FilterLbl: Label '$expand=', Locked = true;
    begin
        RepeatControl(UrlSubPage, FilterLbl, MyLogicalOperators::" ");

        InitConsultationOptionsURL(UrlSubPage, FilterLbl, MyLogicalOperators::" ");
        UrlSubPage += NameSubPage;
    end;
```

<br>

## Filtros en subp√°gina

En las subp√°ginas tambi√©n se pueden hacer filtros. En esta funci√≥n, hemos creado una codeunit como variable externa para que solo nos devuelva los filtros para poder ser a√±adidos a la URL final.

```javascript
    procedure SetUrlSubPage_Filter(NameSubPage: Text; TagField: Text; ValueEquals: Variant; MyLogicalOperators: Enum MyLogicalOperators; MyUrlFilter: Enum MyUrlFilter)
    var
        TxtFilter: Text;
        MyCreateUrl: Codeunit MyCreateUrl;
        WhichLbl: Label '?', Locked = true;
        WhereLbl: Label '=', Locked = true;
    begin
        Clear(MyCreateUrl);
        MyCreateUrl.SetUrlFilter(TagField, ValueEquals, MyLogicalOperators, MyURLFilter);
        UrlSubPage_Filter += MyCreateUrl.GetURL();
        UrlSubPage_Filter := DelChr(UrlSubPage_Filter, WhereLbl, WhichLbl);
    end;
```

<br>

En esta funci√≥n los filtros hemos llamado a esta codeunit como una variable externa para que as√≠ solo nos devuelva los filtros para poder ser a√±adidos a la URL final.

<br>

## Filtrar valores en campos

Se ha creado una funci√≥n para filtrar valores en campos concretos. Lo primero que hacemos es formatear el tipo de valor ya que para muchos endpoint debe tener una formato en concreto y para los objetos de tipo API los formatos tienen que ser espec√≠ficos. Despu√©s inicializamos el filtro y a√±adimos el valor a la variable.

```javascript
    procedure SetUrlFilter(TagField: Text; ValueEquals: Variant; MyLogicalOperators: Enum MyLogicalOperators; MyUrlFilter: Enum MyUrlFilter)
    var
        FilterLbl: Label '$filter=', Locked = true;
        ParenthesisLbl: Label '(%1,%2)', Locked = true;
        Pos: Integer;
        char39: Char;
        TxtValueEquals: Text;
    begin
        char39 := 39;
        TxtValueEquals := Format(ValueEquals);

        case true of
            ValueEquals.IsText:
                TxtValueEquals := char39 + TxtValueEquals + char39;
            ValueEquals.IsDate:
                TxtValueEquals := Format(ValueEquals, 0, '<Year4>-<Month,2>-<Day,2>');
        end;

        InitConsultationOptionsURL(UrlFilter, FilterLbl, MyLogicalOperators);
        case MyUrlFilter of
            MyUrlFilter::contains, MyUrlFilter::startswith, MyUrlFilter::endswith:
                UrlFilter += Format(MyUrlFilter) + StrSubstNo(ParenthesisLbl, TagField, TxtValueEquals);
            else
                UrlFilter += TagField + Format(MyUrlFilter) + TxtValueEquals;
        end;

    end;
```

<br>

Lo primero que hacemos es formatear el tipo de valor ya que para muchos endpoint debe tener una formato en concreto y para los objetos de tipo API los formatos tienes que ser espec√≠ficos.

Despu√©s inicializamos el filtro y por √∫ltimo a√±adimos el valor a la variable.

Como esta funci√≥n se puede utilizar para muchos tipos de filtros hemos creado un enum para determinar la gran mayor√≠a de ellos.

```javascript
enum 51101 "MyUrlFilter"
{
    value(1; " ") { }
    //igual que
    value(2; " eq ") { }
    //menor que
    value(3; " lt ") { }
    //mayor que
    value(4; " gt ") { }
    //no es igual que
    value(5; " ne ") { }
    //inferior a o igual que
    value(6; " le ") { }
    //superior a o igual que
    value(7; " ge ") { }
    //Contiene el valor que
    value(8; "contains") { }
    //Empieza con el valor que
    value(9; "startswith") { }
    //Finaliza por el valor que
    value(10; "endswith") { }
}
```

<br>

Lo √∫nico que tiene el enum es el tipo de filtro con los espacios para que as√≠ podamos ponerlo directamente en la URL

<br>

## Extraer URL

Por √∫ltimo, creamos la funci√≥n para extraer la composici√≥n de la URL que hemos ido creando.

```javascript
   procedure GetURL(): Text
    var
        Pos: Integer;
        TxtLastUrl: Text;
        PosicionInicialLbl: Label '?$', Locked = true;
        SubPageFilterLbl: Label '(%1)', Locked = true;
    begin
        if UrlSubPage_Filter <> '' then begin
            UrlSubPage += StrSubstNo(SubPageFilterLbl, UrlSubPage_Filter);
        end;

        case true of
            StrPos(UrlSubPage, PosicionInicialLbl) <> 0:
                TxtLastUrl := UrlSubPage + UrlTop + UrlSelectFields + UrlFilter;
            StrPos(UrlTop, PosicionInicialLbl) <> 0:
                TxtLastUrl := UrlTop + UrlSubPage + UrlSelectFields + UrlFilter;
            StrPos(UrlSelectFields, PosicionInicialLbl) <> 0:
                TxtLastUrl := UrlSelectFields + UrlSubPage + UrlTop + UrlFilter;
            StrPos(UrlFilter, PosicionInicialLbl) <> 0:
                TxtLastUrl := UrlFilter + UrlSubPage + UrlTop + UrlSelectFields;
        end;

        exit(BaseURL + UrlFilterKeyPageAPI + UrlFuntionPageAPI + TxtLastUrl);
    end;
```

<br>

Como debe tener un orden en concreto lo primero que buscamos es saber cu√°l ser√° el que tenga el ‚Äú?‚Äù y ese lo ponemos primero en los operadores del final de la URL.

Despu√©s a la hora de extraer esa URL la extraemos en el orden que mejor no ira, en este caso primero la Base despu√©s la calve del registro, despu√©s si hay funciones del objeto API y por ultimo los operadores que hemos a√±adido.

<br>

## Conclusi√≥n

En el emocionante viaje de programar en Business Central, cada l√≠nea de c√≥digo cuenta. Estas funciones personalizadas no solo simplifican tus tareas diarias, sino que tambi√©n liberan tu creatividad para enfocarte en lo que realmente importa: construir soluciones incre√≠bles.

<br>

Como de costumbre, el ejemplo completo est√° disponible en [GitHub](https://github.com/Esanpons/Connection-From-BC).

<br>

## M√°s All√° del C√≥digo

Ahora, dejemos por un momento las l√≠neas de c√≥digo y exploremos las posibilidades. ¬øSab√≠as que estas funciones no solo ahorran tiempo, sino que tambi√©n te permiten construir consultas m√°s din√°micas y poderosas?

Imagina poder personalizar f√°cilmente las URL para adaptarse a diferentes escenarios. Desde la selecci√≥n de campos espec√≠ficos hasta la aplicaci√≥n de filtros personalizados, estas funciones te ofrecen un control sin igual sobre tus peticiones API.
